/**
 * File:	modules/KeyManager.ycp
 * Package:	GPG Key Management
 * Summary:	Manages GPG keys in the package manager
 * Authors:	Ladislav Slez√°k <lslezak@novell.com>
 *
 * $Id$
 *
 */

{

textdomain "packager";
module "KeyManager";

import "Report";
import "Directory";
import "String";

// the current state
list<map<string,any> > known_keys = [];

// keys to delete
list<map<string,any> > deleted_keys = [];

// keys to import from a file (file name => trusted flag)
map<string,boolean> import_from_file = $[];

boolean modified = false;

//////////////////////////////////////////////////////////////////////////////

/**
 * Reset the internal state of the module. The current configuration and all
 * changes are deleted.
 */
global void Reset()
{
    known_keys = [];
    deleted_keys = [];
    import_from_file = $[];
    modified = false;
}

/**
 * Read the current configuration from the package manager
 * @return list the current configuration, nil when an error occurr
 */
list<map<string,any> > ReadCurrentKeys()
{
    // read known keys
    list<map<string,any> > ret = Pkg::GPGKeys(false);

    if (ret != nil)
    {
	// add trusted keys
	ret = (list<map<string,any> >)merge(ret, Pkg::GPGKeys(true));
    }

    y2milestone("Read configuration: %1", ret);

    return ret;
}


/**
 * Read the current configuration from the package manager. The previous changes are lost (@see Reset).
 * The target system of the package manager must be initialized before reading GPG keys!
 * @return boolean true on success
 */
global boolean Read()
{
    if (size(known_keys) > 0)
    {
	y2warning("Rereading GPG keys from the package manager");
	Reset();
    }

    known_keys = ReadCurrentKeys();

    if (known_keys == nil)
    {
	known_keys = [];
	return false;
    }

    return true;
}

/**
 * Search a GPG key in the known keys
 * @param key_id ID of the key
 * @return map Data about the key or nil if the key was not found
 */
global map<string,any> SearchGPGKey(string key_id)
{
    map<string,any> ret = nil;

    // search the properties of the key
    foreach(map<string,any> key, known_keys,
	{
	    if (key["id"]:"" == key_id)
	    {
		ret = key;
		break;
	    }
	}
    );

    return ret;
}

/**
 * Apply the changes, update the current status
 * @return boolean true on success
 */
global boolean Write()
{
    if (!modified)
    {
	y2milestone("No change, nothing to write");
	return true;
    }

    y2milestone("Writing key management configuration");

    boolean ret = true;

    // delete the keys marked for removal
    foreach(map<string,any> deleted_key, deleted_keys,
	{
	    y2milestone("Deleting key %1 ('%2')", deleted_key["id"]:"", deleted_key["name"]:"");
	    ret = Pkg::DeleteGPGKey(deleted_key["id"]:"", deleted_key["trusted"]:false) && ret;
	}
    );

    // import the new keys
    foreach(string tmpfile, boolean trusted, import_from_file,
	{
	    y2milestone("Importing key from '%1', trusted: %2", tmpfile, trusted);

	    ret = Pkg::ImportGPGKey(tmpfile, trusted) && ret;
	}
    );

    // modify keys (change the trusted flag)
    // get the current settings
    list<map<string,any> > current_keys = ReadCurrentKeys();
    foreach(map<string,any> key, current_keys,
	{
	    map<string,any> found_key = SearchGPGKey(key["id"]:"");
	    // is the trusted flag different?
	    if (found_key != nil && found_key["trusted"]:false != key["trusted"]:false)
	    {
		y2milestone("Key %1 ('%2'): new trusted state: %3", found_key["id"]:"",
		    found_key["name"]:"", found_key["trusted"]:false);

		// the current state is trusted
		if (found_key["trusted"]:false)
		{
		    // TODO move to the trusted keyring
		}
		else
		{
		    // TODO move from the trusted keyring
		}
	    }
	}
    );


    // all changes are saved, reset them
    Reset();

    // update the current status
    Read();

    return ret;
}

/**
 * Has been something changed?
 * @return boolean true if something has been changed
 */
global boolean Modified()
{
    return modified;
}

/**
 * Return the current keys.
 * @return list list of known GPG keys ($[ "id" : string, "name" : string, "trusted" : boolean ])
 */
global list<map<string,any> > GetKeys()
{
    return known_keys;
}

/**
 * Modify a key
 * @param new_key A map with the key data, the key with the same Key ID is modified. (Only "trusted" flag can be changed.)
 * @return true on success
 */
global boolean ModifyKey(map<string,any> new_key)
{
    boolean ret = true;

    if (new_key != nil)
    {
	// update the key
	known_keys = maplist(map<string,any> k, known_keys,
	    {
		if (k["id"]:"" == new_key["id"]:"")
		{
		    k["trusted"] = new_key["trusted"]:false;
		}

		return k;
	    }
	);
    }

    return ret;
}

/**
 * Delete the key from the package manager
 * @param key_id ID of the key to delete
 * @return boolean true on success
 */
global boolean DeleteKey(string key_id)
{
    if (key_id == nil || key_id == "")
    {
	y2error("Invalid key ID: %1", key_id);
	return false;
    }

    // index of the key
    integer found = nil;
    integer i = 0;

    // copy the key from known keys to the deleted list
    foreach(map<string,any> key, known_keys,
	{
	    if (key["id"]:"" == key_id)
	    {
		deleted_keys = add(deleted_keys, key);
		found = i;
	    }

	    i = i + 1;
	}
    );

    // remove from known keys when found
    if (found != nil)
    {
	known_keys = remove(known_keys, found);
    }

    return (found != nil);
}


/**
 * Import key from a file
 * @param file path to the file
 * @param trusted true if the key is trusted
 * @return map map with the key, nil when import fails (invalid key, not existing file, already imported key...)
 */
global map<string,any> ImportFromFile(string file, boolean trusted)
{
    // check whether the file is valid, copy the file to the tmpdir
    map<string,any> key = Pkg::CheckGPGKeyFile(file);
    y2internal("File content: %1", key);

    if (key != nil && size(key) > 0)
    {
	// update the trusted flag
	key["trusted"] = trusted;
    }
    else
    {
	Report::Error(sformat(_("File '%1'
doesn't contain a valid GPG key."), file));
	return nil;
    }

    boolean known = false;

    // check whether the key is already known
    foreach(map<string,any> k, known_keys,
	{
	    if (k["id"]:"" == key["id"]:"")
	    {
		known = true;
	    }
	}
    );

    if (known)
    {
	// %1 is key ID (e.g. A84EDAE89C800ACA), %2 is key name (e.g. "SuSE Package Signing Key <build@suse.de>")
	Report::Error(sformat(_("Key '%1'
'%2'
is already known, it cannot be added again."), key["id"]:"", key["name"]:""));
	return nil;
    }

    // copy the key to the temporary directory (in fact the keys are imported in Write())
    string tmpfile = sformat("%1/tmp_gpg_key.%2", Directory::tmpdir, size(known_keys));
    string command = sformat("/bin/cp -- '%1' '%2'", String::Quote(file), String::Quote(tmpfile));

    y2internal("Copying the key: %1", command);

    integer out = (integer)SCR::Execute(.target.bash, command);

    if (out != 0)
    {
	Report::Error(_("Cannot copy the key to the temporary directory."));
	return nil;
    }

    // store the import request
    import_from_file = add(import_from_file, tmpfile, trusted);

    // add the new key to the current config
    known_keys = add(known_keys, key);

    return key;
}

/* EOF */
}
