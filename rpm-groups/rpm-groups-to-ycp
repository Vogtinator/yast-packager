#!/usr/bin/perl -w
#
# rpm-groups-to-ycp
#
# Authors: Stefan Hundhammer <sh@suse.de>
#	   Arvin Schnell <arvin@suse.de>
#

use strict;
use English;
use POSIX qw(strftime);

my $infile	= "rpm-groups.txt";
my $outfile	= "rpm-groups.ycp";
my $line;


if ( $#ARGV >= 0 )
{
    $infile = shift @ARGV;
}

if ( $#ARGV >= 0 )
{
    $outfile = shift @ARGV;
}


# Read $infile, split lines into slash-separated components and make a hash
# entry in %tags for each.

my %tags;

open ( SRC, $infile ) or die "FATAL: Can't open $infile";

while ( $line = <SRC> )
{
    chomp $line;	# delete trailing newline, if any
    my $subtag;

    foreach $subtag ( split( '/', $line ) )
    {
	$tags{ $subtag } .= "    // $line\n";
    }
}

close ( SRC );


# Write ycp file header

open ( YCP,  ">" . $outfile ) or die "FATAL: Can't open $outfile";

print "$infile -> $outfile\n";

print YCP <<'HEADER';
#
# Translations for rpm group tags
# Auto generated by rpm-groups-to-ycp
#
{
    textdomain "rpm-groups";
HEADER


# Write ycp file

my $tag;

foreach $tag ( sort keys %tags )
{
    print YCP "\n";

    my $comment = $tags{ $tag };
    my @comment_lines = split( "\n", $comment );

    if ( $#comment_lines > 5 )  # limit comment to 5 lines
    {
        $#comment_lines = 5;    # cut array size
        $comment = join( "\n", @comment_lines ) . "\n    // ...\n";
    }

    $comment .= "    // (All packages)\n"   if ( $comment =~ /zall/ );
    $comment .= "    // (Search result)\n"  if ( $comment =~ /z\?\?/ );

    print YCP $comment;

    print YCP "    s = _(\"$tag\");\n";
}

print YCP "}\n";

close ( YCP );
