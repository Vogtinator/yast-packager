/**
 * File:	packager/inst_dir/instintodir.ycp
 * Module:	System installation
 * Summary:	Installation into  a directory
 * Authors:	Anas Nashif <nashif@suse.de>
 *
 */

{
    textdomain "packager";

    import "Mode";

    Mode::SetMode ("autoinst_config");	// FIXME, messy
    import "Product";
    Mode::SetMode ("normal");		// FIXME, messy

    import "DirInstall";
    import "Installation";

    import "Wizard";
    import "Label";
    import "Popup";

    // wizard stage
    integer current_stage = -1;

    y2debug("creating wizard");
    // create initial dialog
    Wizard::CreateDialog ();
    Wizard::SetDesktopIcon("sw_source");

    // calling inst_proposal

    Installation::destdir = DirInstall::target;
    y2debug("target dir: %1", Installation::destdir);
    symbol ret = `again;
    while (ret == `again)
    {
        ret = (symbol) WFM::CallFunction ("inst_proposal", [true, true, `dirinstall]);
        if (ret == `abort)
        {
            break;
        }
        if (Installation::destdir == "" || Installation::destdir == "/" )
        {
           Popup::Error(_("Select a valid directory for the installation first."));
           ret = `again;
           continue;
        }
        else if (SCR::Read(.target.dir, Installation::destdir ) != nil
                && size((list)SCR::Read(.target.dir, Installation::destdir )) > 0  )
        {
            if(!Popup::YesNo(_("Directory already exists.
Should this directory be used?"))) {
                ret = `again;
                continue;
            }
        }
        else
        {
            break;
        }
    }

    if (ret == `next)
    {
	WFM::CallFunction ("inst_dirinstall", [false, false]);
    }

    Pkg::SourceFinishAll ();
    Pkg::TargetFinish();
    Installation::destdir = "/";
    Wizard::CloseDialog ();

    return ret;
}
