/**
 * Module:		InstURL.ycp
 *
 * Authors:		Klaus Kaempf (kkaempf@suse.de)
 *
 * Purpose:		Convert /etc/install.inf data to URL
 *
 *
 * $Id$
 */

{
    module "InstURL";

    textdomain "packager";

    global boolean is_network = true;

    global string installInf2Url (string extra_dir)
    {
	string options = "";

	string instmode = (string) SCR::Read (.etc.install_inf.InstMode);	// mode
	if (instmode == nil)						// defaults to "CD"
	{
	    instmode = "cd";
	}

	if ((instmode == "cd")					// CD or DVD
	    || (instmode == "dvd"))
	{
	    is_network = false;
	    list devicelist = (list) SCR::Read(.probe.cdrom);
	    foreach (map device, devicelist,
	    {
		string devname = device["dev_name"]:"";
		if (devname != "")
		{
		    if (options != "") options = options + ",";
		    options = options + devname;
		}
	    });
	    if (options != "")
	    {
		options = "devices=" + options;
	    }
	}
	else if (instmode == "hd")						// Harddisk
	{
	    is_network = false;
	    options = "device=/dev/" + (string) SCR::Read (.etc.install_inf.Partition) + ";filesystem=auto";
	}

	string url = instmode + "://";

	if (is_network)
	{
	    string username = (string) SCR::Read (.etc.install_inf.Username);
	    if ((username != nil) && (username != ""))
	    {
		url = url + username;
		string password = (string) SCR::Read (.etc.install_inf.Password);
		if ((password != nil) && (password != ""))
		{
		    url = url + ":";
		    url = url + password;
		}
		url = url + "@";
	    }
	    string server = (string) SCR::Read (.etc.install_inf.Server);
	    if ((server != nil) && (server != ""))
	    {
		url = url + server;
	    }
	} // is_network

	string serverdir = (string) SCR::Read (.etc.install_inf.Serverdir);
	if (((instmode == "hd") || is_network)				// if serverdir needed
	    && ((serverdir != nil) && (serverdir != "")))		// and is valid
	{
	    // for smb mounts it is usual to not have a leading slash
	    if (substring (serverdir, 0, 1) != "/")
		serverdir = "/" + serverdir;

	    url = url + serverdir;
	}
	else
	{
	    url = url + "/";
	}

	if (extra_dir != "")
	{
	    url = url + "/" + extra_dir;
	    y2milestone ("extra_dir url '%1'", url);
	}

	if (is_network)
	{
	    string proxy = (string) SCR::Read (.etc.install_inf.Proxy);
	    if ((proxy != nil) && (proxy != ""))
	    {
		url = url + ";proxy=" + proxy;
	    }
	    string proxyport = (string) SCR::Read (.etc.install_inf.ProxyPort);
	    if ((proxyport != nil) && (proxyport != ""))
	    {
		url = url + ";proxyport=" + proxyport;
	    }
	    string proxyproto = (string) SCR::Read (.etc.install_inf.ProxyProto);
	    if ((proxyproto != nil) && (proxyproto != ""))
	    {
		url = url + ";proxyproto=" + proxyproto;
	    }
	    string proxyuser = (string) SCR::Read (.etc.install_inf.ProxyUser);
	    if ((proxyuser != nil) && (proxyuser != ""))
	    {
		url = url + ";proxyuser=" + proxyuser;
	    }
	    string proxypassword = (string) SCR::Read (.etc.install_inf.ProxyPassword);
	    if ((proxypassword != nil) && (proxypassword != ""))
	    {
		url = url + ";proxypassword=" + proxypassword;
	    }
	} // is_network

	if (options != "")
	{
	    url = url + ";" + options;
	    y2milestone ("options %1", options);
	}
	y2debug ("URL %1", url);
	return url;
    }
}
