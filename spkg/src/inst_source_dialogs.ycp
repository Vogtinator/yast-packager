/**
 * File:		inst_source.ycp
 *
 * Authors:		Klaus Kaempf <kkaempf@suse.de>
 *			Gabriele Strattner <gs@suse.de>
 *			Stefan Schubert <schubi@suse.de>
 *                      Cornelius Schumacher <cschum@suse.de>
 *
 * Purpose:
 * Displays possibilities to install from NFS, CD or partion
 * Do the "mount" for testing the input.
 *
 * $Id$
 */

{
    textdomain "packager";

    import "Label";
    import "URL";

    /**
        Return an HBox with ok and cancel buttons for use by other dialogs.
        @return An HBox term for use in a CreateDialog call.
    */
    global define term dialogButtons() ``{
        return
            `HBox(
                `PushButton( `id( `ok ), `opt( `default ), Label::OKButton() ),
                `HStretch(),
                `PushButton( `id( `cancel ), Label::CancelButton() )
            );
    }

    /**
        Open a dialog for editing of authentification information (basically a
        username and a password.
    */
    global define void editAuthentification() ``{
        UI::OpenDialog(
            `VBox(
                `Label( _("Authentication") ),

                `RadioButtonGroup(`id(`rb),
                    `VBox(
                        `Left( `RadioButton( `id( `anonymous ), _("Anonymous") ) ),
                        `Left( `RadioButton( `id( `account ), _("Account") ) ),
                        `HBox(
                            `HSpacing( 3 ),
                            `TextEntry( `id( `username ), _("User Name") ),
                            `HSpacing( 2 )
                        ),
                        `HBox(
                            `HSpacing( 3 ),
                            `TextEntry( `id( `password ), _("Password") ),
                            `HSpacing( 2 )
                        ),
                        dialogButtons()
                    )
                )
            )
        );

        repeat
        {
            symbol authinput = UI::UserInput();
            y2debug( "Auth Input: %1", authinput );

            if ( authinput == `ok )
            {
                y2debug( "ok clicked" );
            }

        } until ( authinput == `cancel || authinput == `ok );

        UI::CloseDialog();
    }

    /**
        Open a dialog for editing of FTP server information.
    */
    global define void editFtp() ``{
        UI::OpenDialog(
            `VBox(
                `Label( _("FTP Server and Directory") ),
                `TextEntry( `id( `server ), _("Server Name") ),
                `TextEntry( `id( `dir ), _("Directory on Server") ),
                `HBox(
                    `Label( _("Authentication:") ),
                    `Label( `id( `auth ), _("Anonymous") ),
                    `PushButton( `id( `auth_button ), _("Edit...") )
                ),
                dialogButtons()
            )
        );
	UI::SetFocus (`id (`server));

        repeat
        {
            symbol input = UI::UserInput();
            y2debug( "Input: %1", input );

            if ( input == `auth_button )
            {
                editAuthentification();
            }
            else if ( input == `ok )
            {
                y2debug( "ok clicked" );
            }

        } until ( input == `cancel || input == `ok );

        UI::CloseDialog();
    }

    /**
        Get a nfs url.
    */
    global define void getNfsUrl( string host, string urlpath ) ``{
        if ( substring( urlpath, 0, 1 ) == "/" ) {
          urlpath = substring( urlpath, 1 );
        }

        UI::OpenDialog(
            `VBox(
                `Label( _("NFS Server and Directory") ),
                `TextEntry( `id( `server ), _("&Server Name"), host ),
                `TextEntry( `id( `dir ), _("&Directory on Server"), urlpath ),
//                `TextEntry( `id( `mount ), _("&Mount options") ),
                dialogButtons()
            )
        );
	UI::SetFocus (`id (`server));

        string url = "";

        repeat
        {
            symbol input = UI::UserInput();
            y2debug( "Input: %1", input );

            if ( input == `ok )
            {
                string server = UI::QueryWidget( `id( `server ), `Value );
                string dir = UI::QueryWidget( `id( `dir ), `Value );
                string options = UI::QueryWidget( `id( `mount ), `Value );

                url = "nfs://" + server + "/" + dir;
            }

        } until ( input == `cancel || input == `ok );

        UI::CloseDialog();

        return url;
    }

    /**
        Get url for a cd or dvd.
    */
    global define void getCdUrl() ``{
        UI::OpenDialog(
            `VBox(
                `Label( _("CD Installation") ),
                `RadioButtonGroup( `id( `device ),
                    `VBox(
                        `Left( `RadioButton( `id( `cd0 ), "/dev/cdrom", true ) ),
//                        `Left( `RadioButton( `id( `cd1 ), "/dev/cdrom1" ) ),
                        `Left( `RadioButton( `id( `dvd ), "/dev/dvd" ) )
                    )
                ),
                dialogButtons()
            )
        );
	UI::SetFocus (`id (`cd0));

        string url = "";

        repeat
        {
            symbol input = UI::UserInput();
            y2debug( "Input: %1", input );

            if ( input == `ok )
            {
                symbol device = UI::QueryWidget( `id( `device ), `CurrentButton );
                if ( device == `cd0 )
                {
                    url = "cd:///";
                }
                else if ( device == `dvd )
                {
                    url = "dvd:///";
                }
            }

        } until ( input == `cancel || input == `ok );

        UI::CloseDialog();

        return url;
    }

    global define void getNewServerUrl( symbol protocol )
    ``{
      return getServerUrl( protocol, "", "", "", "" );
    }

    /**
        Get a URL for a ftp, http or samba server, optionally inclduing
        user/password information.

        @param protocol type of server, can be `ftp, `http or `smb.
    */
    global define string getServerUrl( symbol type, string host,
                                       string urlpath, string user,
                                       string pass )
    ``{
        if ( substring( urlpath, 0, 1 ) == "/" ) {
          urlpath = substring( urlpath, 1 );
        }

        UI::OpenDialog( `opt( `decorated ),
            `VBox(
                `Label( _("Server and Directory") ),
                `HBox(
                    `HSpacing( 0.5 ),
                    `Frame( _("P&rotocol"),
                        `RadioButtonGroup( `id( `rb_type ),
                            `HBox(
                                `HStretch(),
                                `RadioButton( `id( `ftp ), _("&FTP") ),
                                `HStretch(),
                                `RadioButton( `id( `http ), _("H&TTP") ),
                                `HStretch(),
                                `RadioButton( `id( `samba ), _("&Samba") ),
                                `HStretch()
                            )
                        )
                    ),
                    `HSpacing( 0.5 )
                ),
                `TextEntry( `id( `server ), _("Server &Name"), host ),
                `TextEntry( `id( `dir ), _("&Directory on Server"), urlpath ),
                `HBox(
                    `HSpacing( 0.5 ),
                    `Frame( _("Au&thentication"),
                        `VBox(
                            `Left( `CheckBox( `id( `anonymous ),
                                              `opt( `notify ), _("&Anonymous"),
                                              size( user ) != 0 ) ),
                            `HBox(
                                `TextEntry( `id( `username ), _("&User Name"),
                                            user )
                            ),
                            `HBox(
                                `Password( `id( `password ), _("&Password"),
                                           pass )
                            )
                        )
                    ),
                    `HSpacing( 0.5 )
                ),
                dialogButtons()
            )
        );

        UI::ChangeWidget( `id( `rb_type ), `CurrentButton, type );
	UI::SetFocus (`id (`server));

        string url = "";

        repeat
        {
            boolean anonymous = UI::QueryWidget( `id( `anonymous ), `Value );
            UI::ChangeWidget( `id( `username ), `Enabled, !anonymous );
            UI::ChangeWidget( `id( `password ), `Enabled, !anonymous );

            symbol input = UI::UserInput();
            y2debug( "Input: %1", input );

            if ( input == `ok )
            {
                url = "";
                symbol type = UI::QueryWidget( `id( `rb_type), `CurrentButton );
                if ( type == `ftp ) {
                    url = "ftp://";
                } else if ( type == `http ) {
                    url = "http://";
                } else if ( type == `samba ) {
                    url = "smb://";
                }

                if ( !anonymous )
                {
                    string user = UI::QueryWidget( `id( `username ), `Value );
                    string password = UI::QueryWidget( `id( `password ), `Value );

                    if ( size( user ) != 0 ) {
                        url = url + user;
                        if ( size( password ) != 0 ) {
                            url = url + ":" + password;
                        }
                        url = url + "@";
                    }
                }

                string server = UI::QueryWidget( `id( `server ), `Value );
                string directory = UI::QueryWidget( `id( `dir ), `Value );

                url = url + server + "/" + directory;


            }
        } until ( input == `cancel || input == `ok );

        UI::CloseDialog();

        return url;
    }

    /**
     * Let the user put in a directory url.
     *
     * @param default  initial default
     * @return directory url on success, empty string when the user
     *   canceled the dialog.
    */
    global define string getDirectoryUrl( string default ) ``{

        UI::OpenDialog(
            `VBox(
                `Label( _("Local Directory") ),
                `HBox(
                    `TextEntry( `id( `dir ), _("&Directory Path") ),
                    `PushButton( `id( `browse ), _("&Browse...") )
                ),
                dialogButtons()
            )
        );

        UI::ChangeWidget( `id( `dir ), `Value, default );
	UI::SetFocus (`id (`dir));

        string url = "";

        repeat
        {
            symbol input = UI::UserInput();

            if ( input == `browse )
            {
                url = UI::QueryWidget( `id( `dir ), `Value );

		// dialog caption
 		string result = UI::AskForExistingDirectory( url, _("Local Directory") );
                if ( result != nil )
                {
                    UI::ChangeWidget( `id( `dir ), `Value, result );
                }
            }
            else if ( input == `ok )
            {
                url = UI::QueryWidget( `id( `dir ), `Value );

                url = "dir://" + url;
            }
            else if ( input == `cancel )
            {
                url = "";
            }

        } until ( input == `cancel || input == `ok );

        UI::CloseDialog();

        return url;
    }

    global define string getUrl( string default ) ``{
        UI::OpenDialog(
            `VBox(
		/* Label text */
                `Label( _("Select Type of URL:") ),
                `RadioButtonGroup( `id(`url_type),
                    `VBox(
			/* RadioButton label */
                        `Left( `RadioButton( `id( `dir ), _("D&irectory"), true ) ),
			/* RadioButton label */
                        `Left( `RadioButton( `id( `ftp ), "&FTP" ) ),
			/* RadioButton label */
                        `Left( `RadioButton( `id( `http ), "&HTTP" ) ),
			/* RadioButton label */
                        `Left( `RadioButton( `id( `samba ), "&Samba" ) ),
			/* RadioButton label */
                        `Left( `RadioButton( `id( `nfs ), "&NFS" ) ),
			/* RadioButton label */
                        `Left( `RadioButton( `id( `cd ), "&CD" ) ),
			/* RadioButton label */
                        `Left( `RadioButton( `id( `dvd ), "&DVD" ) )
                    )
                ),
                dialogButtons()
            )
        );

        string url = "";
	UI::SetFocus (`id (`dir));

        symbol input = UI::UserInput();

        if ( input == `ok )
        {
            symbol type = UI::QueryWidget( `id( `url_type), `CurrentButton );

            UI::CloseDialog();

            if ( type == `ftp || type == `http || type == `samba ) {
                url = getNewServerUrl( type );
            } else if ( type == `dir ) {
                url = getDirectoryUrl( default );
            } else if ( type == `nfs ) {
                url = getNfsUrl( "", "" );
            } else if ( type == `cd || type == `dvd ) {
                url = getCdUrl();
            }
        } else {
            UI::CloseDialog();
        }

        return url;
    }

    global define string editUrl( string url ) ``{
        if ( size( url ) == 0 ) return getUrl( "" );

        map parsedUrl = URL::Parse( url );

        string protocol = parsedUrl[ "scheme" ]:"";
        string urlpath = parsedUrl[ "path" ]:"";

        string newUrl = "";

        if ( protocol == "ftp" || protocol == "http" || protocol == "smb" ) {
            string host = parsedUrl[ "host" ]:"";
            string user = parsedUrl[ "user" ]:"";
            string pass = parsedUrl[ "pass" ]:"";
            symbol type = `ftp;
            if ( protocol == "http" ) type = `http;
            else if ( protocol == "smb" ) type = `samba;
            newUrl = getServerUrl( type, host, urlpath, user, pass );
        } else if ( protocol == "nfs" ) {
            string host = parsedUrl[ "host" ]:"";
            newUrl = getNfsUrl( host, urlpath );
        } else if ( protocol == "dir" || protocol == "file" ) {
            newUrl = getDirectoryUrl( urlpath );
        } else if ( protocol == "cd" || protocol == "dvd" ) {
            newUrl = getCdUrl();
        } else {
            y2error( "Unsupported URL: %1", url );
        }

        return newUrl;
    }
}
