/* 
 * 
 *
 * $Id$
 */

{
  Include( `package_utils() );
  
  // Menuentry for the YaST2 menu
  if (Args() == [ "get_menuentry" ]) 
      return [ 
	      "sw_single", $[
			     `menuentry    : UI(_("Install/Remove packages")),
			     `arguments    : [ ],
			     `widget       : `RichText(
						       UI(_( "<P>Launch this module to install additional packages or
remove installed ones.
For installing commercial software also launch this module and select
set 'pay' from list of package sets.
</P>
<P>You need to be logged in as <i>root</i> in order to
do this.</P>"))),
			     `codefragment : nil ]
      ];

  UI(``{
      /**
       * Create a typical installation wizard dialog.
       */
      OpenDialog(
		 `opt(`defaultsize),
		 `VBox(
		       `ReplacePoint( `id(`image ), `Image( `suseheader, "SuSE") ),
		       `HBox(
			     `HWeight( 30, `ReplacePoint( `id(`helpSpace), `RichText(`id(`help), ""))),
			     `HSpacing(1),
			     `HWeight( 70, `VBox(
						 `VSpacing(0.2),
						 `Left(`Heading(`id(`title), _("YaST2\nInitializing ..."))),
						 `HVCenter(`ReplacePoint(`id(`contents), `Empty())),
						 `HBox(
						       // "back" button: Return to the previous dialog
						       `PushButton(`id(`back), _("&Back")),

						       `HCenter(
								// "abort" button: The installation will be aborted
								`ReplacePoint( `id(`rep_abort), `PushButton(`id(`abort), _("&Abort Installation")))
								),

						       // "next" button: Proceed to the next dialog
						       `ReplacePoint( `id(`rep_next), `PushButton(`id(`next), _("&Next")) )
						       ),
						 `VSpacing(0.2)
						 )
				       ),
			     `HSpacing(1)
			     )
		       )
                 );


      /*
       * Define how the general framework for the installation wizard should
       * look like. This Macro creates and show a dialog.
       */
      define SetWizardContents(string title, term contents, string helptext,
			       boolean has_back, boolean has_next) ``{
	  ChangeWidget(`id(`back), `Enabled, has_back);
	  ChangeWidget(`id(`next), `Enabled, has_next);
	  ChangeWidget(`id(`help), `Value, helptext);
	  ChangeWidget(`id(`title), `Value, title);
	  ReplaceWidget(`id(`contents), contents);
	  SetFocus( `id(`next) );
      };
      
      define size_text (integer bytes) ``{
	  float whole = tofloat (bytes) / 1024.0;
	  // KiloByte abbreviated
	  string unit = _("KB");
	  if (whole > 1024.0)
          {
	      whole = whole / 1024.0;
	      // MegaByte abbreviated
	      unit = _("MB");
	      if (whole > 1024.0)
              {
		  whole = whole / 1024.0;
		  // GigaByte abbreviated
		  unit = _("GB");
              }
          }
	  return tostring (whole, 2) + " " + unit;
      };

      /*
       * Small dialog that has a cancel and continue button that
       * appears just befor the video mode is tested.
       */
      define ContinueCancel(string message,
			    string continue_button_message,
			    string cancel_button_message ) ``{
	  OpenDialog(
		     `opt(`decorated),
		     `HBox(
			   `HSpacing(1),
			   `VBox(
				 `VSpacing(0.2),
				 `Label(message),
				 `HBox(
				       `PushButton(`id(`continue), `opt(`default),continue_button_message),
				       `HSpacing(2),
				       `PushButton(`id(`cancel),   cancel_button_message)
				       ),
				 `VSpacing(0.2)
				 ),
			   `HSpacing(1)
			   )
		     );
	  any r = UserInput();
	  CloseDialog();
	  return ( r == `continue );
      };

      /*
       * Are your sure?
       */

      define YesOrNo(any message, string yes_button_message, string no_button_message) ``{
          OpenDialog(
		     `opt(`decorated),
		     `HBox(
			   `HSpacing(1),
			   `VBox(
				 `VSpacing(0.2),
				 `Label(message),
				 `HBox(
				       `PushButton(`id(`yes), `opt(`default), yes_button_message),
				       `HSpacing(2),
				       `PushButton( no_button_message )
				       ),
				 `VSpacing(0.2)
				 ),
			   `HSpacing(1)
			   )
		     );
          any ret = UserInput();
          CloseDialog();
          return (ret == `yes);
      };

      /*
       * Are your sure? with possibiltity to specify default-Button ( values: `no_default, `yes_default )
       */

      define YesNoOpt(any message, string yes_button_message, string no_button_message, symbol def_symbol) ``{
	  term yes_button = `empty();
	  term no_button = `empty();
	  
	  if ( def_symbol == `no_default )
	  {
	      yes_button = `PushButton( `id(`yes),  yes_button_message );
	      no_button  = `PushButton( `id(`no_button), `opt(`default), no_button_message );
	  }
	  else
	  {
	      yes_button = `PushButton(`id(`yes), `opt(`default), yes_button_message);
	      no_button = `PushButton( `id(`no_button), no_button_message );
	  }
	  
          OpenDialog(
		     `opt(`decorated),
		     `HBox(
			   `HSpacing(1),
			   `VBox(
				 `VSpacing(0.2),
				 `Label(message),
				 `HBox(
				       yes_button,
				       `HSpacing(2),
				       no_button
				       ),
				 `VSpacing(0.2)
				 ),
			   `HSpacing(1)
			   )
		     );
          any ret = UserInput();
          CloseDialog();
          return (ret == `yes);
      };


      /*
       * Displays a message to the user in a small dialog with an OK
       * button.
       */

      define DisplayMessage(string message) ``{
          // ok pushbutton: confirm the dialog
          OpenDialog(
		     `opt(`decorated),
		     `HBox(
			   `HSpacing(1),
			   `VBox(
				 `VSpacing(0.2),
				 `Label(message),
				 `PushButton(`opt(`default), _("&OK")),
				 `VSpacing(0.2)
				 ),
			   `HSpacing(1)
			   )
		     );
          UserInput();
          CloseDialog();
      };

  });

  // map user_settings contains:
  // - install_info -> module inst_rpmcopy
  // - delete_info  -> module inst_rpmcopy
  // - continue_mode -> module inst_rpmcopy
  // - language -> module inst_rpmcopy
  
  map user_settings = $[];
  
  string language = UI( `GetLanguage() );

  string default_language = "en";
  string default_architecture = "i386";

  map lang2yast1 = $[
		     "en_GB"    : "english",
		     "en_US"    : "english",
		     "en"	 : "english",
		     "de_DE"    : "german",
		     "de_CH"    : "german",
		     "de" 	 : "german",
		     "br_FR"    : "french",
		     "fr_FR"    : "french",
		     "fr_CH"    : "french",
		     "fr"       : "french",
		     "it_IT"    : "italian",
		     "es_ES"    : "spanish",
		     "nl_NL"    : "dutch",
		     "pt_PT"    : "portuguese",
		     "pt_BR"    : "brazilian",
		     "hu_HU"    : "hungarian",
		     "pl_PL"    : "polish",
		     "el_GR"    : "greek",
		     "ru_RU.KOI8-R" : "russian",
		     "cs_CZ"    : "czech",
		     "sk_SK"    : "slovak"
  ];

  string  long_language 	= lookup(lang2yast1, language, "english");
  
  // informations needed in inst_rpmcopy
  user_settings = add( user_settings, "language", language );
  user_settings = add( user_settings, "continue_mode", false );
  user_settings = add( user_settings, "post_install", true );
  
  // !!!!!!!!!!!!!!! ONLY FOR TESTING !!!!!!!!!!!!!!!!!!!!
  // user_settings = add(user_settings, "test_mode", true );
  // !!!!!!!!!!!!!!! ONLY FOR TESTING !!!!!!!!!!!!!!!!!!!!

  //
  // Initialize PKGINFO server
  //

  string architecture = SCR(`Read(.probe.architecture));
  user_settings = add (user_settings, "architecture", architecture );
  
  // Initialisation of pkginfo in post_install mode -> inst_s390net

  list partition = [];


  // get information about diskspace ( used/free space on every partition )
  partition = SCR( `Read(.run.df ) );
  
  _debug ( "DF DF DF: ", partition );

  // filter out headline and other invalid entries 
  partition = filter( `part, partition, ``( substring ( lookup( part, "name" ), 0, 1 ) == "/" ) );

  // pkginfo expects names of partitions without "/" in front ( exception: "/" itself ) 
  list part_input = [];

  foreach( `part, partition, ``{
      map part_info = $[];
      if ( lookup( part, "name" ) != "/" )
      {
	  part_info = add( part_info, "name", substring( lookup( part, "name" ), 1 ) );
      }
      else
      {
	  part_info = add( part_info, "name",  lookup( part, "name" ) );
      }
      part_info = add( part_info, "used",  tointeger(lookup( part, "used" )) );
      part_info = add( part_info, "free",  tointeger(lookup( part, "free" )) );

      part_input = add( part_input, part_info );
  });

  _debug( "SW: part_input: ", part_input ); 

  /*
   * main dialog loop
   */
  
  list dialog = [
                ``CallFunction (`inst_source( false, true ) ),
		``CallFunction (`inst_sw_single( `post_install, `not_only_checked, part_input ) ),
                ``CallFunction (`inst_rpmcopy( false, false ) ),
		``CallFunction (`inst_suseconfig( false, false ) )
  ];

  integer id = 0;
  any result = `next;

  while ((id >= 0) && (id < size (dialog))) {

      if ( id == 1 )
      {
	  // mount CD 1 BEFORE initializing pkginfo-Server
	  // (needed to get commom.pkd)
	  map resMap = ChangeCD( 1, architecture);
	  boolean go_on = lookup ( resMap, "success", false );

	  if ( !go_on )
	  {
	      // quit dialog
	      UI( `CloseDialog() );      
	      return `cancel;
	  }

	  string instSource = GetInstSource(1);
	  string infopath =  instSource + "setup/descr";
	  string dudir = instSource + "setup/du/du.dir";
	  
	  UI(`OpenDialog(`opt(`decorated ),`Label(_("Reading package information; One moment please..."))));
	  
	  any retval = PKGINFO (`setEnvironment( $["packageinfopath":infopath, "common.pkd":"common.pkd",
					  "language":long_language,  "dudir":dudir,
					  "partition":part_input, "rootpath":"/",
					  "yastpath":"/var/lib/YaST",
					  "update":true,
					  "memoptimized":true]
					 ));

	  UI(`CloseDialog());	  

	  _debug( "SW: Return  PKGINFO (`setEnvironment()", retval );
      }

      
    result = eval(select(dialog, id));

    _debug ( "RESULT: ", select(dialog, id), result );
    
    if (result == `cancel || result == `abort)
      break;
    if (result == `auto)
	break;			// break here (last module inst_suseconfig returns `auto)
    else if (result == `next )		
      id = id + 1;
    else if (result == `ok )	// inst_sw_single returns `ok
    {
	 // setting user_setting.install_info for packages which have to be installed
      list install_info =  PKGINFO ( `getInstallSet() );
      // _debug ( "Writing install_info: ", install_info );
      user_settings = add ( user_settings , "install_info", install_info );

      list delete_info =  PKGINFO ( `getDeleteSet() );
      // _debug ( "Writing delete_info: ", delete_info );
      user_settings = add ( user_settings , "delete_info", delete_info );

      _debug( "SW: Usersettings: ", user_settings );

      if ( install_info != [] || delete_info != [] )
      {
	 id = id + 1;  
      }
      else
      {
	  _debug( "Nothing selected" );
      }
    }
    else if (result == `back || result == `cancel_single )	
      id = id - 1;

  }

  UI( `CloseDialog() );

  // umount
  
  Shell ("/bin/umount /var/adm/mount");

  return `next;
}
