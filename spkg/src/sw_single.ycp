/* 
 * 
 *
 * $Id$
 */

{
  Include( `package_utils() );

  map user_settings = $[];
  
  string architecture = SCR(`Read(.probe.architecture));
  user_settings = add (user_settings, "architecture", architecture );

  string msg_str = "";
  
  string msg_str11 =   UI(_( "<P>Launch this module to install additional packages or
remove installed ones.
For installing commercial software also launch this module and select
set 'pay' from list of package sets.
</P>" ));
  string msg_str12 = UI(_( "<p>Please insert <b>CD 1</b> for CD installation.<br>
Data required for installation over network or from harddisk
will be asked in separate dialog." ));
  
  string msg_str2 = UI(_( "<P>You need to be logged in as <i>root</i> in order to
do this.</P>"));

  if ( architecture != "s390" )
  {
      msg_str = msg_str11 + msg_str12 + msg_str2;
  }
  else
  {
      msg_str = msg_str11 + msg_str2;
  }
  
  // Menuentry for the YaST2 menu
  if (Args() == [ "get_menuentry" ]) 
      return [ 
	      "sw_single", $[
			     "menuentry"    : UI(_("Install/Remove packages")),
			     "arguments"    : [ ],
			     "widget"       : `RichText(msg_str),
			     "codefragment" : nil ]
      ];

  UI(``{
      /**
       * Create a typical installation wizard dialog.
       */
      OpenDialog(
		 `opt(`defaultsize),
		 `VBox(
		       `ReplacePoint( `id(`image ), `Image( `suseheader, "SuSE") ),
		       `HBox(
			     `HWeight( 30, `ReplacePoint( `id(`helpSpace), `RichText(`id(`help), ""))),
			     `HSpacing(1),
			     `HWeight( 70, `VBox(
						 `VSpacing(0.2),
						 `Left(`Heading(`id(`title), _("YaST2\nInitializing ..."))),
						 `HVCenter(`ReplacePoint(`id(`contents), `Empty())),
						 `HBox(
						       // "back" button: Return to the previous dialog
						       `PushButton(`id(`back), _("&Back")),

						       `HCenter(
								// "abort" button: The installation will be aborted
								`ReplacePoint( `id(`rep_abort), `PushButton(`id(`abort), _("&Abort Installation")))
								),

						       // "next" button: Proceed to the next dialog
						       `ReplacePoint( `id(`rep_next), `PushButton(`id(`next), _("&Next")) )
						       ),
						 `VSpacing(0.2)
						 )
				       ),
			     `HSpacing(1)
			     )
		       )
                 );


      /*
       * Define how the general framework for the installation wizard should
       * look like. This Macro creates and show a dialog.
       */
      define SetWizardContents(string title, term contents, string helptext,
			       boolean has_back, boolean has_next) ``{
	  ChangeWidget(`id(`back), `Enabled, has_back);
	  ChangeWidget(`id(`next), `Enabled, has_next);
	  ChangeWidget(`id(`help), `Value, helptext);
	  ChangeWidget(`id(`title), `Value, title);
	  ReplaceWidget(`id(`contents), contents);
	  SetFocus( `id(`next) );
      };
      
      define size_text (integer bytes) ``{
	  float whole = tofloat (bytes) / 1024.0;
	  // KiloByte abbreviated
	  string unit = _("KB");
	  if (whole > 1024.0|| whole < -1024.0)
          {
	      whole = whole / 1024.0;
	      // MegaByte abbreviated
	      unit = _("MB");
	      if (whole > 1024.0|| whole < -1024.0)
              {
		  whole = whole / 1024.0;
		  // GigaByte abbreviated
		  unit = _("GB");
              }
          }
	  return tostring (whole, 2) + " " + unit;
      };

      /*
       * Small dialog that has a cancel and continue button that
       * appears just befor the video mode is tested.
       */
      define ContinueCancel(string message,
			    string continue_button_message,
			    string cancel_button_message ) ``{
	  OpenDialog(
		     `opt(`decorated),
		     `HBox(
			   `HSpacing(1),
			   `VBox(
				 `VSpacing(0.2),
				 `Label(message),
				 `HBox(
				       `PushButton(`id(`continue), `opt(`default),continue_button_message),
				       `HSpacing(2),
				       `PushButton(`id(`cancel),   cancel_button_message)
				       ),
				 `VSpacing(0.2)
				 ),
			   `HSpacing(1)
			   )
		     );
	  any r = UserInput();
	  CloseDialog();
	  return ( r == `continue );
      };

      /*
       * Are your sure?
       */

      define YesOrNo(any message, string yes_button_message, string no_button_message) ``{
          OpenDialog(
		     `opt(`decorated),
		     `HBox(
			   `HSpacing(1),
			   `VBox(
				 `VSpacing(0.2),
				 `Label(message),
				 `HBox(
				       `PushButton(`id(`yes), `opt(`default), yes_button_message),
				       `HSpacing(2),
				       `PushButton( no_button_message )
				       ),
				 `VSpacing(0.2)
				 ),
			   `HSpacing(1)
			   )
		     );
          any ret = UserInput();
          CloseDialog();
          return (ret == `yes);
      };

      /*
       * Are your sure? with possibiltity to specify default-Button ( values: `no_default, `yes_default )
       */

      define YesNoOpt(any message, string yes_button_message, string no_button_message, symbol def_symbol) ``{
	  term yes_button = `empty();
	  term no_button = `empty();
	  
	  if ( def_symbol == `no_default )
	  {
	      yes_button = `PushButton( `id(`yes),  yes_button_message );
	      no_button  = `PushButton( `id(`no_button), `opt(`default), no_button_message );
	  }
	  else
	  {
	      yes_button = `PushButton(`id(`yes), `opt(`default), yes_button_message);
	      no_button = `PushButton( `id(`no_button), no_button_message );
	  }
	  
          OpenDialog(
		     `opt(`decorated),
		     `HBox(
			   `HSpacing(1),
			   `VBox(
				 `VSpacing(0.2),
				 `Label(message),
				 `HBox(
				       yes_button,
				       `HSpacing(2),
				       no_button
				       ),
				 `VSpacing(0.2)
				 ),
			   `HSpacing(1)
			   )
		     );
          any ret = UserInput();
          CloseDialog();
          return (ret == `yes);
      };


      /*
       * Displays a message to the user in a small dialog with an OK
       * button.
       */

      define DisplayMessage(string message) ``{
          // ok pushbutton: confirm the dialog
          OpenDialog(
		     `opt(`decorated),
		     `HBox(
			   `HSpacing(1),
			   `VBox(
				 `VSpacing(0.2),
				 `Label(message),
				 `PushButton(`opt(`default), _("&OK")),
				 `VSpacing(0.2)
				 ),
			   `HSpacing(1)
			   )
		     );
          UserInput();
          CloseDialog();
      };

  });

  // map user_settings contains:
  // - install_info -> module inst_rpmcopy
  // - delete_info  -> module inst_rpmcopy
  // - continue_mode -> module inst_rpmcopy
  // - language -> module inst_rpmcopy
  
  string language = UI( `GetLanguage() );

  string default_language = "en";

  map lang2yast1 = $[
		     "en_GB"    : "english",
		     "en_US"    : "english",
		     "en"	 : "english",
		     "de_DE"    : "german",
		     "de_CH"    : "german",
		     "de" 	 : "german",
		     "br_FR"    : "french",
		     "fr_FR"    : "french",
		     "fr_CH"    : "french",
		     "fr"       : "french",
		     "it_IT"    : "italian",
		     "es_ES"    : "spanish",
		     "nl_NL"    : "dutch",
		     "pt_PT"    : "portuguese",
		     "pt_BR"    : "brazilian",
		     "hu_HU"    : "hungarian",
		     "pl_PL"    : "polish",
		     "el_GR"    : "greek",
		     "ru_RU.KOI8-R" : "russian",
		     "cs_CZ"    : "czech",
		     "sk_SK"    : "slovak"
  ];

  string  long_language 	= lookup(lang2yast1, language, "english");
  
  user_settings = add( user_settings, "language", language );
  user_settings = add( user_settings, "continue_mode", false );
  user_settings = add( user_settings, "post_install", true );

  // default is CD installation
  map installMap = $[];
  installMap = add (installMap, "bootmode", "CD" );
  installMap = add (installMap, "cdrom", "cdrom" );
  user_settings = add( user_settings, "installMap", installMap );

  // !!!!!!!!!!!!!!! ONLY FOR TESTING !!!!!!!!!!!!!!!!!!!!
  // user_settings = add(user_settings, "test_mode", true );
  // !!!!!!!!!!!!!!! ONLY FOR TESTING !!!!!!!!!!!!!!!!!!!!
  
  //
  // Get informations for initialisation of PKGINFO server
  //

  string infopath =  "/var/adm/mount/suse/setup/descr";
  string dudir = "/var/adm/mount/suse/setup/du/du.dir";

  list partition = [];

  // get information about diskspace ( used/free space on every partition )
  partition = SCR( `Read(.run.df ) );
  _debug ( "DF DF DF: ", partition );
 
  // filter out headline and other invalid entries 
  partition = filter( `part, partition, ``( substring ( lookup( part, "name" ), 0, 1 ) == "/" ) );

  // pkginfo expects names of partitions without "/" in front ( exception: "/" itself ) 
  list part_input = [];
  
  foreach( `part, partition, ``{
      map part_info = $[];
      if ( lookup( part, "name" ) != "/" )
      {
	  part_info = add( part_info, "name", substring( lookup( part, "name" ), 1 ) );
      }
      else
      {
	  part_info = add( part_info, "name",  lookup( part, "name" ) );
      }
      part_info = add( part_info, "used",  tointeger(lookup( part, "used" )) );
      part_info = add( part_info, "free",  tointeger(lookup( part, "free" )) );

      part_input = add( part_input, part_info );
  });

  _debug( "SW: part_input: ", part_input ); 
   
 
  
  /*
   * main dialog loop
   */
  
  list dialog = [
		 `inst_source( false, true ),
		 `inst_sw_single( ),
		 `inst_rpmcopy( false, false ),
		 `inst_suseconfig( false, false )
  ];

  integer id = 0;
  any result = `next;
  boolean going_forward = true;
  
  // if CD 1 is already inserted, skip module inst_source
  if ( lookup( ChangeCD( 1, architecture, false ), "success", false ) || size(Args() ) > 0 )
  {
      id = id + 1;
  }

  // TODO: evtl. anderes Popup bei Abort
  while ((id >= 0) && (id < size (dialog))) {

      if ( symbolof (select( dialog, id )) == `inst_sw_single )
      {
	  // mount CD 1 BEFORE initializing pkginfo-Server (needed to get commom.pkd)
	  map resMap = ChangeCD( 1, architecture, true);
	  boolean go_on = lookup ( resMap, "success", false );

	  if ( !go_on )
	  {
	      // quit dialog
	      UI( `CloseDialog() );      
	      return `cancel;
	  }

	  string instSource = GetInstSource(1);
	  string infopath =  instSource + "setup/descr";
	  string dudir = instSource + "setup/du/du.dir";
	  
	  UI(`OpenDialog(`opt(`decorated ),`Label(_("Reading package information; One moment please..."))));
	  
	  any retval = PKGINFO (`setEnvironment( $["packageinfopath":infopath, "common.pkd":"common.pkd",
						  "language":long_language,  "dudir":dudir,
						  "partition":part_input, "rootpath":"/",
						  "yastpath":"/var/lib/YaST",
						  "update":true,
						  "memoptimized":true]
						 ));

	  UI(`CloseDialog());	  

	  _debug( "SW: Return  PKGINFO (`setEnvironment()", retval );

          // There could be a list of packages which have to be installed.
	  // ( Defined in an ascii-file, which path is given via argument. )
	  if ( size(Args() ) > 0 )
	  {
	      string pathname = Args(0);

	      map|any installMap = $[];
	      
	      string fileCommand = "test -f " + pathname;
	      boolean file_found = (Shell(fileCommand) == 0);

	      if ( !file_found )
	      {
		  // It could be a single package which have to be installed.
		  list install_list = [];
		  install_list = add ( install_list, Args(0) );
		  installMap = add ( installMap, "install", install_list );
		  y2log ( .milestone, "sw_single", 1,
		      sformat ( "package %1 will be installed", Args(0) ));
	      }
	      else
	      {
		  y2log ( .milestone, "sw_single", 1,
		      sformat ( "Reading file %1", pathname ) );
		  installMap = Read ( pathname );
	      }
	      y2log ( .milestone, "sw_single", 2,
		      sformat ( "installMap: %1", installMap ) );
      

	      if ( installMap == nil || size ( installMap ) == 0 )
	      {
		  string message = UI(_("Error while reading onfiguration-file."));
		  UI(`DisplayMessage(message));
		  ret_single = `cancel;
	      }
	      else
	      {
		  list installList = lookup ( installMap, "install", [] );
		  list deleteList = lookup ( installMap, "delete", [] );
		  boolean userInput = lookup ( installMap, "userInput", false );

		  y2log ( .milestone, "sw_single)", 2,
			  sformat ( "installList: %1", installList ) );
		  y2log ( .milestone, "sw_single)", 2,
			  sformat ( "deleteList: %1", deleteList ) );
		  y2log ( .milestone, "sw_single)", 2,
			  sformat ( "userInput: %1", userInput ) );

	  
		  foreach( `package, installList, ``{
		      any r = PKGINFO( `selectInstall( package ));
		      _debug( "PKGINFO selectInstall: ", package );
		  } );           

		  foreach( `package, deleteList, ``{
		      any r = PKGINFO( `selectDelete( package ) );
		      _debug( "PKGINFO selectDelete: ", package );
		  } );
	       
		  // Checking dependecies
		  map pac_depends = PKGINFO(`getDependencies());
	   
		  list(map) or_depends = lookup( pac_depends, "OR" );
		  _debug( "OR Dependencies:", or_depends );
		  list(map) xor_depends = lookup( pac_depends, "XOR" );
		  _debug( "XOR Dependencies:", xor_depends );

		  if ( userInput == true )
		  {
		      result = CallFunction( `inst_sw_single( `post_install,
							      `not_only_checked,
							      part_input ) );
		  }
		  else
		  {
		      if ( or_depends != [] || xor_depends != [] )
		      {
			  // There is an unresolved dependency -> call
			  // single-package-selection to solve it.
			  result = CallFunction( `inst_sw_single( `post_install,
								  `only_check,
								  part_input) );
		      }
		      else
		      {
			  // install anyway
			  result = `ok;
		      }
		  }
	      }
	  }
	  else
	  {
	      // Calling inst_sw_single
	      result = CallFunction( `inst_sw_single( `post_install,
						      `not_only_checked,
						      part_input ) );

	  }
      }
      else
      {
	  result = CallFunction( select(dialog, id) );
      }
      _debug ( "RESULT: ", select(dialog, id), result );

      if (result == `cancel || result == `abort)
      {
	  // dialog loop ends
	  id = size( dialog ) +1; 
      }
      else if (result == `auto)
      {
	  // inst_source returns `auto if there is already inserted a CD -> go to inst_sw_single
	  // inst_suseconfig returns `auto -> last module, dialog loop ends
	  id = id + 1;			
      }
      else if (result == `next )		
      {
	  id = id + 1;
      }
      else if (result == `ok )	// inst_sw_single returns `ok
      {
	  // setting user_setting.install_info for packages which have to be installed
	  list install_info =  PKGINFO ( `getInstallSet() );
	  // _debug ( "Writing install_info: ", install_info );
	  user_settings = add ( user_settings , "install_info", install_info );

	  list delete_info =  PKGINFO ( `getDeleteSet() );
	  // _debug ( "Writing delete_info: ", delete_info );
	  user_settings = add ( user_settings , "delete_info", delete_info );

	  _debug( "SW: Usersettings: ", user_settings );

	  if ( install_info != [] || delete_info != [] )
	  {
	      id = id + 1;  
	  }
	  else
	  {
	      _debug( "Nothing selected" );
	      // dialog loop ends
	      id = id+3;
	  }
      }
      else if (result == `back || result == `cancel_single )
      {
	  going_forward = false;
	  id = id - 1;
      }
  }

  UI(`CloseDialog());
  
  // umount CD
  Shell ("/bin/umount /var/adm/mount");
  
  return `next;
}
